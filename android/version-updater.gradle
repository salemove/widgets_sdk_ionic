/**
 * This build script is responsible for reading and updating the project related versions for integration with CI/CD flows
 */

ext {
    gradlePropertiesFilePath = "version.properties"
    widgetsSdkVersionTagInProperties = 'dependency.gliaAndroidWidgetsSdk.version'
    widgetsSdkVersionTag = 'gliaAndroidWidgetsSdkVersion'
    telemetryLibVersionTagInProperties = 'dependency.telemetryLib.version'
    telemetryLibVersionTag = 'gliaTelemetryLibVersion'
}

exposeGradlePropertiesToBuildScripts()

def exposeGradlePropertiesToBuildScripts() {
  def propertiesFile = project.file('version.properties')
  def properties = new Properties()
  if (propertiesFile.exists()) {
    propertiesFile.withInputStream { properties.load(it) }
  }
  ext[widgetsSdkVersionTag] = properties.getProperty(widgetsSdkVersionTagInProperties)
  ext[telemetryLibVersionTag] = properties.getProperty(telemetryLibVersionTagInProperties)
}

/**
 * A task class for editing the Widgets SDK version in version.properties
 * Example of usage from terminal:
 * ./gradlew glia-widgets-ionic:saveGliaAndroidWidgetsSdkVersion --sdkVersion=3.2.0
 * ./gradlew glia-widgets-ionic:saveGliaTelemetryLibVersion --sdkVersion=1.0.2
 */
class UpdateVersionProperty extends DefaultTask {
  private String newVersion
  private String propertyKey

  @Option(option = "sdkVersion", description = "Version that should be overridden in configuration files")
  void setSdkVersion(String sdkVersion) {
    this.newVersion = sdkVersion
  }

  void setPropertyKey(String propertyKey) {
    this.propertyKey = propertyKey
  }

  @TaskAction
  void write() {
    def propertiesFile = project.file('version.properties')
    def properties = new Properties()
    if (propertiesFile.exists()) {
      propertiesFile.withInputStream { properties.load(it) }
    }
    properties.setProperty(propertyKey, newVersion)
    propertiesFile.withOutputStream { properties.store(it, null) }
  }
}

tasks.register('saveGliaAndroidWidgetsSdkVersion', UpdateVersionProperty) {
  it.setPropertyKey(project.widgetsSdkVersionTagInProperties)
}
tasks.register('saveGliaTelemetryLibVersion', UpdateVersionProperty) {
  it.setPropertyKey(project.telemetryLibVersionTagInProperties)
}
